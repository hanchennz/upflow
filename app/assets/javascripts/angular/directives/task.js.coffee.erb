#= depend_on_asset 'directives/task.html'

upflow.directive 'upTask', (Task) ->
  restrict: 'E'
  scope:
    deleteTask: '='
    displayedTask: '='
    repeatByOptions: '='
    task: '='
  templateUrl: '<%= asset_path("directives/task.html") %>'
  link: (scope, element, attrs) ->
    scope.displayed = (task) -> scope.displayedTask == task if scope.displayedTask

    scope.updateTaskDescription = (description, task) ->
      Task.update { task: {description: description}, id: task.id }
      , (success) ->
        task.oldDescription = task.description
      , (error) ->
        task.description = task.oldDescription

    scope.updateTaskRepeat = ->
      Task.update { task: { repeat_by: scope.task.repeat_by }, id: scope.task.id }
      , (success) ->
        _(scope.task).extend { next_due_date: success.next_due_date, rank: success.rank, color: success.color }

    scope.toggleTaskComplete = ->
      completed_at = if scope.task.completed_at then null else new Date()
      Task.update { task: { completed_at: completed_at }, id: scope.task.id }
      , (success) ->
        _(scope.task).extend { completed_at: success.completed_at, rank: success.rank, color: success.color }

    scope.toggleTaskCompleteTitle = ->
      if scope.task.completed_at
        'Reactivate Task'
      else
        'Complete Task'

    scope.toggleXEditMode = (form) -> form.$show()
    scope.editEnabled = (form) -> form.$visible
    scope.convertToDate = (date) -> new Date(date)
